mapping com.github.jmodel.mapper.sample.json2json.Sample_JSON2JSON { 
	
	from: JSON 
	to: JSON     
	                            
	/**
	 * From Policy to PolicyEntity
     */    
	Policy => PolicyEntity { 
		"PolicyType" => Type;    
		EffectiveDate => EffDate;  
		ExpiryDate => ExpDate; 
		   
		/** 
		 * From Policy Customer List to Party List
		 */		
		PolicyCustomerList[] => PartyList[] { 
			IF Customer.PartyType=="20" THEN
				"1" => ContactType; 
			ELSEIF Customer.PartyType=="21" THEN
		    	"2" => ContactType; 
			ELSE
				Customer.PartyType => ContactType;    	
			ENDIF   
		        
		    /**
		 	 * Supplement Contact Info
		     */
			Customer.PartyContactList[] => . {       
				:IsPrimaryContact=="Y"
				Name => ContactName;  
			};	     
		    
		    /**
		 	 * Supplement Address Info
		     */
			Customer.PartyAddressList[] => . {     
				:IsPrimaryAddress=="Y"
				(Country + ";" + Province + ";" + City + ";" + AddressLine1 + ";" + AddressLine1) => ContactAddress;			
			};	 
		};	   
		             
		/**
		 * Append Sales Channel Info to Party List
		 */    
		SalesChannelOrg => PartyList[]+ {  
			"2" => ContactType; 
			PartyName => PartyName;
			
			/**
			 * Supplement Contact Info of Sales Channel Organization
			 */ 
			PartyContactList[] => . {
				:IsPrimaryContact=="Y"
				Name => ContactName;   
			};
			
			/**
			 * Supplement Address Info of Sales Channel Organization
			 */
			PartyAddressList[] => . {     
				:IsPrimaryAddress=="Y"
				(Country + ";" + Province + ";" + City + ";" + AddressLine1 + ";" + AddressLine1) => ContactAddress;			
			};	 
			
		};
	};	  
	 
	/**
	 * From Policy LOB to PolicyEntity
     */         
	Policy.PolicyLobList[] => PolicyEntity {	       
		ProductId => ProductCode; 
        
        /**
	 	 * From Risk List of Policy LOB to Insured List of Policy Entity
         */      
		PolicyRiskList[] => InsuredList[] { 
			"RiskType" => Type;
			Country => CountryCode;
			"3" => InsuredCategory;
			(Country + ";" + Province + ";" + City + ";" + District + ";" + Address)  => InsuredName;
			
			/**
	 	 	 * Construct Policy Limit Info
         	 */ 
			. => PolicyLimit { 
				:SumInsured != null      
				"PolicyLimitType" => Type;           
				
				/**
	 	 	 	 * Construct Policy Limit Value Info
         	 	 */
				. => PolicyLimitValue[] {
					"PolicyLimitValueType" => Type;
					"0" => IsAllowance;
					SumInsured => LimitValue;
					"AOA" => ModeType;
					"AMOUNT" => ValueType;
				};
			};      
			
			/**
	 	 	 * From Policy Coverage List of Risk to Coverage List
             */         
			PolicyCoverageList[] => CoverageList[] {   
				"PolicyCoverageType" => Type;
				(##InsuredId + "-3") => PolicyRiskNo;       
				#ProductId => ProductCode; 			  	   
				 
				/**
	 	 	 	 * Append Policy Coverage List of LOB to Coverage List
                 */                   
				#PolicyCoverageList[] => .+ {      
					"PolicyCoverageType" => Type;
					#ProductId => ProductCode;
				}; 
			};							      
		};		 
	};	
	   
}